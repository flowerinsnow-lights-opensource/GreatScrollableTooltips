buildscript() {
    repositories() {
        mavenCentral()
        gradlePluginPortal()
        maven() {
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven() {
            url 'https://maven.minecraftforge.net/'
        }
    }
    dependencies() {
        classpath('net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT')
        classpath('com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:4.0.4')
        classpath('org.spongepowered:mixingradle:0.6-SNAPSHOT')
    }
}
apply('plugin': 'net.minecraftforge.gradle.forge')
apply('plugin': 'com.github.johnrengelman.shadow')
apply('plugin': 'org.spongepowered.mixin')

version = '3.1.0'
group = 'online.flowerinsnow.greatscrollabletooltips' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'great_scrollable_tooltips'

minecraft() {
    version = '1.11-13.19.1.2199'
    runDir = 'run'
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = 'snapshot_20161220'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories() {
    mavenLocal()
    mavenCentral()
    maven() {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven() {
        url = 'https://maven.pkg.github.com/flowerinsnow-lights-opensource/GreatScrollableTooltips'
        credentials() {
            username = "${System.getenv('GITHUB_USERNAME')}"
            password = "${System.getenv('GITHUB_TOKEN')}"
        }
    }
}

configurations() {
    shade
    compile.extendsFrom(shade)
}

dependencies() {
    shade("org.spongepowered:mixin:${version_mixin}") {
        exclude('module': 'guava')
        exclude('module': 'commons-io')
        exclude('module': 'gson')
    }
}

processResources() {
    // this will ensure that this task is redone when the versions change.
    inputs.property('version', project.version)
    inputs.property('mcversion', project.minecraft.version)

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include('mcmod.info')
                
        // replace version and mcversion
        expand('version': project.version, 'mcversion': project.minecraft.version)
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude('mcmod.info')
    }
}

tasks.withType(JavaCompile).configureEach() {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

shadowJar() {
    exclude('LICENSE.txt')
    exclude('dummyThing')

    manifest() {
        attributes(['TweakClass': 'online.flowerinsnow.greatscrollabletooltips.tweaker.GreatScrollableTooltipsTweaker'])
    }

    configurations = [project.configurations.shade]

    classifier = ''
}

reobf() {
    shadowJar {
    }
}

mixin() {
    add(sourceSets.main, "great-scrollable-tooltips-refmap.json")
}
