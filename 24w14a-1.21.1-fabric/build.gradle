plugins() {
    id('fabric-loom').version('1.8.0-alpha.13')
    id('maven-publish')
}

version = project.mod_version
group = project.maven_group

base() {
	archivesName = project.archives_base_name
}

repositories() {
	maven() {
		url = 'https://www.cursemaven.com/'
	}

	maven() {
		url = 'https://maven.pkg.github.com/flowerinsnow-lights-opensource/GreatScrollableTooltips'
		credentials() {
			username = "${System.getenv('GITHUB_USERNAME')}"
			password = "${System.getenv('GITHUB_TOKEN')}"
		}
	}

	maven() {
		url = 'https://maven.terraformersmc.com/releases/'
	}
}

dependencies() {
	// To change the versions see the gradle.properties file
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	
	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

	modImplementation("com.terraformersmc:modmenu:${modmenu_version}")
	modImplementation('curse.maven:legendary-tooltips-fabric-542478:4938105')
	include(api("online.flowerinsnow.greatscrollabletooltips:common:${common_module_version}"))
	include(api("com.electronwill.night-config:core:${night_config_version}"))
	include(api("com.electronwill.night-config:toml:${night_config_version}"))
}

processResources() {
	LinkedHashMap<String, ?> props = ['version' : project.version]
	props.forEach(inputs::property)

	filesMatching('fabric.mod.json') {
		expand(props)
	}
}

tasks.withType(JavaCompile).configureEach() {
	it.options.release = 17
}

java() {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar() {
	from(file('../LICENSE'))
	from(file('../NOTICE'))
}

// configure the maven publication
publishing() {
	publications() {
		mavenJava(MavenPublication) {
			from(components.java)
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories() {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

loom() {
	accessWidenerPath = file('src/main/resources/great-scrollable-tooltips.accesswidener')
}
